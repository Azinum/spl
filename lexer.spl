// lexer.spl

// Token_type
enum u64 (
  T_EOF,

  T_IDENTIFIER,
  T_NUMBER,
  T_CSTRING,
  T_ASSIGN,
  T_COMMA,
  T_AT,
  T_DEREF,
  T_ADD,
  T_SUB,
  T_MUL,
  T_DIV,
  T_DIVMOD,
  T_LSHIFT,
  T_RSHIFT,
  T_LT,
  T_GT,
  T_AND,
  T_LOGICAL_NOT,
  T_OR,
  T_EQ,
  T_NEQ,
  T_COLON,
  T_SEMICOLON,
  T_CONST,
  T_LET,
  T_MEMORY,
  T_PRINT,
  T_STATIC_ASSERT,
  T_INCLUDE,
  T_FN,
  T_ARROW,
  T_WHILE,
  T_IF,
  T_ELSE,
  T_LEFT_P,
  T_RIGHT_P,
  T_LEFT_BRACKET,
  T_RIGHT_BRACKET,
  T_LEFT_CURLY,
  T_RIGHT_CURLY,
  T_STORE64,
  T_STORE32,
  T_STORE16,
  T_STORE8,
  T_LOAD64,
  T_LOAD32,
  T_LOAD16,
  T_LOAD8,
  T_SIZEOF,
  T_ENUM,
  T_ALIAS,
  T_CAST,
  T_STRUCT,

  // built-in types
  T_NONE,
  T_ANY,
  T_PTR,
  T_UNSIGNED64,
  T_UNSIGNED32,
  T_CSTR,

  MAX_TOKEN_TYPE
);

let token_type_str : cstr : MAX_TOKEN_TYPE "";

struct Token (
  buffer   : ptr,
  length   : u64,
  type     : u64,
  v        : u64,
  filename : ptr,
  source   : ptr,
  line     : u64,
  column   : u64
);

let token : Token 0;

// struct Lexer {
let l.filename : cstr "";
let l.source : cstr "";
let l.index : ptr cast ptr 0;
let l.line : u64 0;
let l.column : u64 0;
let l.status : u64 0;
// };

fn token_init(token : any, buffer : any, length : u64, type : u64, filename : any, source : any) -> none {
  = + token Token.buffer   buffer;
  = + token Token.length   length;
  = + token Token.type     type;
  = + token Token.v        0;
  = + token Token.filename filename;
  = + token Token.source   source;
  = + token Token.line     1;
  = + token Token.column   1;
}

fn copy_token_into_memory(mem : any) -> none {
  = + mem Token.buffer   + @token Token.buffer;
  = + mem Token.length   + @token Token.length;
  = + mem Token.type     + @token Token.type;
  = + mem Token.v        + @token Token.v;
  = + mem Token.filename + @token Token.filename;
  = + mem Token.source   + @token Token.source;
  = + mem Token.line     + @token Token.line;
  = + mem Token.column   + @token Token.column;
}

fn token_print(token : ptr) -> none {
  let tmp tmp_it;
  let p tmp_push_cstr;

  let buffer   # + token Token.buffer;
  let length   # + token Token.length;
  let type     # + token Token.type;
  let value    # + token Token.v;
  let filename # + token Token.filename;
  let source   # + token Token.source;
  let line     # + token Token.line;
  let column   # + token Token.column;

  if eq filename 0 = @filename "none";

  p("buffer   = "); tmp_push_str(buffer, length);
  p("\n");
  p("length   = "); tmp_push_u64(length);
  p("\n");
  p("type     = "); tmp_push_u64(type);
  p("\n");
  p("filename = "); p(filename);
  p("\n");
  p("line     = "); tmp_push_u64(line);
  p("\n");
  p("column   = "); tmp_push_u64(column);
  p("\n");

  putsn(STDOUT_FILENO, tmp, - tmp_it tmp);
  = @tmp_it tmp;
}

fn lexer_token_type -> u64 load64 + @token Token.type;

fn lexer_read_symbol -> none {
  while or or or or or
    is_alpha(load8 l.index)
    is_digit(load8 l.index)
    is_extended_ascii(load8 l.index)
    eq load8 l.index '_'
    eq load8 l.index '-'
    eq load8 l.index '.' {
    = @l.index + 1 l.index;
    = @l.column + 1 l.column;
  }
  = + @token Token.length - l.index load64 + @token Token.buffer;

  let buffer cast cstr load64 + @token Token.buffer;
  let length : u64 load64 + @token Token.length;

  if eq 0 strncmp(buffer, "print", length) {
    = + @token Token.type T_PRINT;
  }
  else if eq 0 strncmp(buffer, "static_assert", length) {
    = + @token Token.type T_STATIC_ASSERT;
  }
  else if eq 0 strncmp(buffer, "include", length) {
    = + @token Token.type T_INCLUDE;
  }
  else if eq 0 strncmp(buffer, "const", length) {
    = + @token Token.type T_CONST;
  }
  else if eq 0 strncmp(buffer, "let", length) {
    = + @token Token.type T_LET;
  }
  else if eq 0 strncmp(buffer, "memory", length) {
    = + @token Token.type T_MEMORY;
  }
  else if eq 0 strncmp(buffer, "fn", length) {
    = + @token Token.type T_FN;
  }
  else if eq 0 strncmp(buffer, "while", length) {
    = + @token Token.type T_WHILE;
  }
  else if eq 0 strncmp(buffer, "if", length) {
    = + @token Token.type T_IF;
  }
  else if eq 0 strncmp(buffer, "else", length) {
    = + @token Token.type T_ELSE;
  }
  else if eq 0 strncmp(buffer, "store64", length) {
    = + @token Token.type T_STORE64;
  }
  else if eq 0 strncmp(buffer, "store32", length) {
    = + @token Token.type T_STORE32;
  }
  else if eq 0 strncmp(buffer, "store16", length) {
    = + @token Token.type T_STORE16;
  }
  else if eq 0 strncmp(buffer, "store8", length) {
    = + @token Token.type T_STORE8;
  }
  else if eq 0 strncmp(buffer, "load64", length) {
    = + @token Token.type T_LOAD64;
  }
  else if eq 0 strncmp(buffer, "load32", length) {
    = + @token Token.type T_LOAD32;
  }
  else if eq 0 strncmp(buffer, "load16", length) {
    = + @token Token.type T_LOAD16;
  }
  else if eq 0 strncmp(buffer, "load8", length) {
    = + @token Token.type T_LOAD8;
  }
  else if eq 0 strncmp(buffer, "and", length) {
    = + @token Token.type T_AND;
  }
  else if eq 0 strncmp(buffer, "not", length) {
    = + @token Token.type T_LOGICAL_NOT;
  }
  else if eq 0 strncmp(buffer, "or", length) {
    = + @token Token.type T_OR;
  }
  else if eq 0 strncmp(buffer, "eq", length) {
    = + @token Token.type T_EQ;
  }
  else if eq 0 strncmp(buffer, "neq", length) {
    = + @token Token.type T_NEQ;
  }
  else if eq 0 strncmp(buffer, "lshift", length) {
    = + @token Token.type T_LSHIFT;
  }
  else if eq 0 strncmp(buffer, "rshift", length) {
    = + @token Token.type T_RSHIFT;
  }
  else if eq 0 strncmp(buffer, "sizeof", length) {
    = + @token Token.type T_SIZEOF;
  }
  else if eq 0 strncmp(buffer, "enum", length) {
    = + @token Token.type T_ENUM;
  }
  else if eq 0 strncmp(buffer, "alias", length) {
    = + @token Token.type T_ALIAS;
  }
  else if eq 0 strncmp(buffer, "cast", length) {
    = + @token Token.type T_CAST;
  }
  else if eq 0 strncmp(buffer, "struct", length) {
    = + @token Token.type T_STRUCT;
  }
  else if eq 0 strncmp(buffer, "none", length) {
    = + @token Token.type T_NONE;
  }
  else if eq 0 strncmp(buffer, "any", length) {
    = + @token Token.type T_ANY;
  }
  else if eq 0 strncmp(buffer, "ptr", length) {
    = + @token Token.type T_PTR;
  }
  else if eq 0 strncmp(buffer, "u64", length) {
    = + @token Token.type T_UNSIGNED64;
  }
  else if eq 0 strncmp(buffer, "u32", length) {
    = + @token Token.type T_UNSIGNED32;
  }
  else if eq 0 strncmp(buffer, "cstr", length) {
    = + @token Token.type T_CSTR;
  }
  else {
    = + @token Token.type T_IDENTIFIER;
  }
}

fn lexer_read_number -> none {
  while or is_hex(load8 l.index) eq load8 l.index 'x' {
    = @l.index + 1 l.index;
    = @l.column + 1 l.column;
  }
  = + @token Token.length - l.index load64 + @token Token.buffer;
  = + @token Token.type T_NUMBER;
}

fn lexer_error(message : cstr) -> none {
  if eq l.status NoError {
    let tmp tmp_it;
    // a bit noisey, but this will do for now
    // TODO(lucas): varadic functions to the rescue
    tmp_push_cstr("[lex-error]: ");
    tmp_push_cstr(load64 + @token Token.filename);
    tmp_push_cstr(":");
    tmp_push_u64(load64 + @token Token.line);
    tmp_push_cstr(":");
    tmp_push_u64(load64 + @token Token.column);
    tmp_push_cstr(": ");
    tmp_push_cstr(message);
    putsn(STDERR_FILENO, tmp, - tmp_it tmp);
    = @tmp_it tmp;
    = @l.status Error;
  }
}

fn next -> none {
  = + @token Token.buffer l.index;
  = + @token Token.length 1;
  = + @token Token.line l.line;
  = + @token Token.column l.column;
}

fn lexer_next -> none {
  let done 0;
  let ch 0;
  while not done {
    next();
    = @ch load8 load64 + @token Token.buffer;
    = + @token Token.filename l.filename;
    = + @token Token.source l.source;
    = + @token Token.column l.column;
    = @l.index + 1 l.index;
    = @l.column + 1 l.column;

    if eq ch 13 { // `\r`
      = @l.column 1;
      if neq 10 load8 l.index {
        = @l.line + 1 l.line
      }
      = + @token Token.column l.column;
    }
    else if eq ch 10 { // `\n`
      = @l.line + 1 l.line;
      = @l.column 1;
      = + @token Token.column l.column;
    }
    else if eq ch '/' {
      if eq '/' load8 l.index {
        while and neq load8 l.index 10 neq load8 l.index 0 {
          = @l.index + 1 l.index;
          = @l.column + 1 l.column;
          = + @token Token.column l.column;
        }
      }
      else {
        = + @token Token.type T_DIV;
        = @done 1;
      }
    }
    else if eq ch '"' {
      let delimiter ch;
      let loop_done 0;
      while eq loop_done 0 {
        if eq load8 l.index 0 {
          lexer_error("unfinished string\n");
          = + @token Token.type T_EOF;
          = @loop_done 1;
          = @done 1;
        }
        else if eq load8 l.index 92 { // `\\`
          if eq load8 + l.index 1 '0' {
            store8 l.index 0;
            = @l.index + 1 l.index;
          }
          if eq load8 + l.index 1 'n' {
            store8 l.index 10;
            = @l.index + 1 l.index;
          }
          = @l.index + 1 l.index;
          = @l.column + 1 l.column;
        }
        else if eq load8 l.index delimiter {
          = @loop_done 1;
        }
        else {
          = @l.index + 1 l.index;
          = @l.column + 1 l.column;
        }
      }
      = + @token Token.buffer + 1 load64 + @token Token.buffer;
      = @l.column + 1 l.column;
      = + @token Token.type T_CSTRING;
      = + @token Token.length - l.index load64 + @token Token.buffer;
      = @l.index + 1 l.index;
      = @done 1;
    }
    else if eq ch '=' {
      = + @token Token.type T_ASSIGN;
      = @done 1;
    }
    else if eq ch ',' {
      = + @token Token.type T_COMMA;
      = @done 1;
    }
    else if eq ch '@' {
      = + @token Token.type T_AT;
      = @done 1;
    }
    else if eq ch '#' {
      = + @token Token.type T_DEREF;
      = @done 1;
    }
    else if eq ch '+' {
      = + @token Token.type T_ADD;
      = @done 1;
    }
    else if eq ch '-' {
      if eq load8 l.index '>' {
        = @l.index + 1 l.index;
        = @l.column + 1 l.column;
        = + @token Token.length + 1 load64 + @token Token.length;
        = + @token Token.type T_ARROW;
        = @done 1;
      }
      else {
        = + @token Token.type T_SUB;
        = @done 1;
      }
    }
    else if eq ch '*' {
      = + @token Token.type T_MUL;
      = @done 1;
    }
    else if eq ch '%' {
      = + @token Token.type T_DIVMOD;
      = @done 1;
    }
    else if eq ch '<' {
      = + @token Token.type T_LT;
      = @done 1;
    }
    else if eq ch '>' {
      = + @token Token.type T_GT;
      = @done 1;
    }
    else if eq ch ':' {
      = + @token Token.type T_COLON;
      = @done 1;
    }
    else if eq ch ';' {
      = + @token Token.type T_SEMICOLON;
      = @done 1;
    }
    else if eq ch '(' {
      = + @token Token.type T_LEFT_P;
      = @done 1;
    }
    else if eq ch ')' {
      = + @token Token.type T_RIGHT_P;
      = @done 1;
    }
    else if eq ch '[' {
      = + @token Token.type T_LEFT_BRACKET;
      = @done 1;
    }
    else if eq ch ']' {
      = + @token Token.type T_RIGHT_BRACKET;
      = @done 1;
    }
    else if eq ch '{' {
      = + @token Token.type T_LEFT_CURLY;
      = @done 1;
    }
    else if eq ch '}' {
      = + @token Token.type T_RIGHT_CURLY;
      = @done 1;
    }
    else if or or or eq ch ' ' eq ch 9 eq ch 11 eq ch 12 { // ` `, `\t`, `\v`, `\f`
    }
    else if eq ch 0 {
      = + @token Token.type T_EOF;
      = @done 1;
    }
    else if eq ch 39 { // `\'`
      = @ch load8 l.index;
      = + @token Token.buffer l.index;
      = @l.index + 1 l.index;
      = @l.column + 1 l.column;
      if neq load8 l.index 39 {
        = + @token Token.type T_EOF;
        lexer_error("missing closing `'`\n");
      }
      else {
        = @l.index + 1 l.index;
        = @l.column + 1 l.column;
        = + @token Token.v ch;
        = + @token Token.type T_NUMBER;
        = + @token Token.length 1;
      }
      = @done 1;
    }
    else {
      if or or is_alpha(ch) is_extended_ascii(ch) eq ch '_' {
        lexer_read_symbol();
        = @done 1;
      }
      else if is_digit(ch) {
        lexer_read_number();
        let number str_to_u64(cast cstr load64 + @token Token.buffer, load64 + @token Token.length);
        = + @token Token.v number;
        = @done 1;
      }
      else {
        let tmp tmp_it;
        tmp_push_cstr("unrecognized token `");
        tmp_push_str(load64 + @token Token.buffer, load64 + @token Token.length);
        tmp_push_cstr("`\n");
        tmp_push_byte(0); // null-terminator
        lexer_error(cast cstr tmp);
        = @tmp_it tmp;
        = + @token Token.type T_EOF;
        = @done 1;
      }
    }
  }
  = + @token Token.line l.line;
}

fn lexer_init(filename : any, source : any) -> none {
  // initialize lexer state
  = @l.filename filename;
  = @l.source source;
  = @l.index source;
  = @l.line 1;
  = @l.column 1;
  = @l.status NoError;

  // initialize lexer token
  = + @token Token.buffer source;
  = + @token Token.length 0;
  = + @token Token.type T_EOF;
  = + @token Token.v 0;
  = + @token Token.filename filename;
  = + @token Token.source source;
  = + @token Token.line 1;
  = + @token Token.column 1;

  = + @token_type_str * T_EOF           sizeof cstr "T_EOF";
  = + @token_type_str * T_IDENTIFIER    sizeof cstr "T_IDENTIFIER";
  = + @token_type_str * T_NUMBER        sizeof cstr "T_NUMBER";
  = + @token_type_str * T_CSTRING       sizeof cstr "T_CSTRING";
  = + @token_type_str * T_ASSIGN        sizeof cstr "T_ASSIGN";
  = + @token_type_str * T_COMMA         sizeof cstr "T_COMMA";
  = + @token_type_str * T_AT            sizeof cstr "T_AT";
  = + @token_type_str * T_DEREF         sizeof cstr "T_DEREF";
  = + @token_type_str * T_ADD           sizeof cstr "T_ADD";
  = + @token_type_str * T_SUB           sizeof cstr "T_SUB";
  = + @token_type_str * T_MUL           sizeof cstr "T_MUL";
  = + @token_type_str * T_DIV           sizeof cstr "T_DIV";
  = + @token_type_str * T_DIVMOD        sizeof cstr "T_DIVMOD";
  = + @token_type_str * T_LSHIFT        sizeof cstr "T_LSHIFT";
  = + @token_type_str * T_RSHIFT        sizeof cstr "T_RSHIFT";
  = + @token_type_str * T_LT            sizeof cstr "T_LT";
  = + @token_type_str * T_GT            sizeof cstr "T_GT";
  = + @token_type_str * T_AND           sizeof cstr "T_AND";
  = + @token_type_str * T_LOGICAL_NOT   sizeof cstr "T_LOGICAL_NOT";
  = + @token_type_str * T_OR            sizeof cstr "T_OR";
  = + @token_type_str * T_EQ            sizeof cstr "T_EQ";
  = + @token_type_str * T_NEQ           sizeof cstr "T_NEQ";
  = + @token_type_str * T_COLON         sizeof cstr "T_COLON";
  = + @token_type_str * T_SEMICOLON     sizeof cstr "T_SEMICOLON";
  = + @token_type_str * T_CONST         sizeof cstr "T_CONST";
  = + @token_type_str * T_LET           sizeof cstr "T_LET";
  = + @token_type_str * T_MEMORY        sizeof cstr "T_MEMORY";
  = + @token_type_str * T_PRINT         sizeof cstr "T_PRINT";
  = + @token_type_str * T_INCLUDE       sizeof cstr "T_INCLUDE";
  = + @token_type_str * T_FN            sizeof cstr "T_FN";
  = + @token_type_str * T_ARROW         sizeof cstr "T_ARROW";
  = + @token_type_str * T_WHILE         sizeof cstr "T_WHILE";
  = + @token_type_str * T_IF            sizeof cstr "T_IF";
  = + @token_type_str * T_ELSE          sizeof cstr "T_ELSE";
  = + @token_type_str * T_LEFT_P        sizeof cstr "T_LEFT_P";
  = + @token_type_str * T_RIGHT_P       sizeof cstr "T_RIGHT_P";
  = + @token_type_str * T_LEFT_BRACKET  sizeof cstr "T_LEFT_BRACKET";
  = + @token_type_str * T_RIGHT_BRACKET sizeof cstr "T_RIGHT_BRACKET";
  = + @token_type_str * T_LEFT_CURLY    sizeof cstr "T_LEFT_CURLY";
  = + @token_type_str * T_RIGHT_CURLY   sizeof cstr "T_RIGHT_CURLY";
  = + @token_type_str * T_STORE64       sizeof cstr "T_STORE64";
  = + @token_type_str * T_STORE32       sizeof cstr "T_STORE32";
  = + @token_type_str * T_STORE16       sizeof cstr "T_STORE16";
  = + @token_type_str * T_STORE8        sizeof cstr "T_STORE8";
  = + @token_type_str * T_LOAD64        sizeof cstr "T_LOAD64";
  = + @token_type_str * T_LOAD32        sizeof cstr "T_LOAD32";
  = + @token_type_str * T_LOAD16        sizeof cstr "T_LOAD16";
  = + @token_type_str * T_LOAD8         sizeof cstr "T_LOAD8";
  = + @token_type_str * T_SIZEOF        sizeof cstr "T_SIZEOF";
  = + @token_type_str * T_ENUM          sizeof cstr "T_ENUM";
  = + @token_type_str * T_ALIAS         sizeof cstr "T_ALIAS";
  = + @token_type_str * T_CAST          sizeof cstr "T_CAST";
  = + @token_type_str * T_STRUCT        sizeof cstr "T_STRUCT";

  // built-in types
  = + @token_type_str * T_NONE          sizeof cstr "T_NONE";
  = + @token_type_str * T_ANY           sizeof cstr "T_ANY";
  = + @token_type_str * T_PTR           sizeof cstr "T_PTR";
  = + @token_type_str * T_UNSIGNED64    sizeof cstr "T_UNSIGNED64";
  = + @token_type_str * T_UNSIGNED32    sizeof cstr "T_UNSIGNED32";
  = + @token_type_str * T_CSTR          sizeof cstr "T_CSTR";
}
